# -*- mode: coffee; tab-width: 2; -*-
#
class CardView extends Backbone.View

  @tagName: 'div'
  @className: 'card'

  constructor: (params) ->
    super(params)
    @model.bind 'change', @render
    @template = _.template($('#card-template').html())
    @el = $(@template(@model.toJSON()))
    @img = @el.find('img')

    @el.draggable
      position: 0,
      scope: 'cards',
      snap: '.card',
      scroll: true,
      revert: 'invalid',
      containment: 'window',
      snapMode: 'inner',
      zIndex: 99999,
      stack: '.card',
      # start: ->
      #   $('.card-browser .container').css('overflow-y', 'visible')
      # stop: ->
      #   $('.card-browser .container').css('overflow-y', 'scroll')

    @el.bind 'click', @clicked
    @el.bind 'contextmenu', @clicked

    # LEGACY
    @el.data('game-object',this)
    @element = @el


  clicked: (e) =>
    e.preventDefault()
    e.stopPropagation()

    if e.button == 2
      @model.toggle_tapped()

      if @model.covered()
        Message.action "is tapping a card."
      else
        Message.action "is tapping '#{@model.name()}'."
    else
      unless @model.covered()
        new CardDetailView(model: @model, position: @el.offset())

  render: =>
    @el.find('.debug').text(@model.order())
    @show_image()
    @show_tapping()
    @_render_overlay()
    @el.show()
    this

  _render_overlay: =>
    if @model.counters()?
      @$('.counters').fadeIn().text(@model.counters())

    if @model.toughness()? or @model.power()?
      @$('.power').fadeIn().text("#{@model.power() || 0}/#{@model.toughness() || 0}")

  show_image: =>
    if @model.covered()
      @img.attr('src','/images/back.jpg')
    else
      @img.attr('src',@model.image())

  show_tapping: =>
    if @model.tapped()
      $(@el).addClass('tapped')
    else
      $(@el).removeClass('tapped')


window.CardView = CardView
