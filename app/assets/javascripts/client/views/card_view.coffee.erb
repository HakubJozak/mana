# -*- mode: coffee; tab-width: 2; -*-
#
class CardView extends Backbone.View

  @tagName: 'div'
  @className: 'card'

  constructor: (params) ->
    super(params)
    @model.bind 'change', @render
    @template = _.template($('#card-template').html())
    @el = $(@template(@model.toJSON()))
    @img = @el.find('img')

    @el.draggable
      position: 0,
      scope: 'cards',
      snap: '.card',
      scroll: true,
      revert: 'invalid',
      containment: 'body',
      snapMode: 'inner',
      zIndex: 9999,
      stack: '.card',
      start: =>
        # HACK
        $('.overlay').trigger('mouseleave')

    @el.bind 'click', @clicked
    @el.bind 'contextmenu', @clicked

    # LEGACY
    @el.data('game-object',this)
    @element = @el


  clicked: (e) =>
    e.preventDefault()
    e.stopPropagation()

    if e.button == 2
      @model.toggle_tapped()
    else
      @show_detail()

  show_detail: =>
    unless @model.covered()
      img = @$('img')
      w = img.width() * 2
      h = img.height() * 2
      pos = img.offset()
      remove = -> $(this).click -> $(this).fadeOut -> $(this).remove()

      img.clone()
         .addClass('detail')
         .css('position', 'absolute')
         .css('top',  pos.top)
         .css('left', pos.left)
         .appendTo('body')
         .animate({ width: "#{w}px", height: "#{h}px", top: "#{pos.top - 50}px", left: "#{pos.left - 50}px" }, remove)
         .draggable()

  render: =>
    @el.find('.debug').text(@model.collection.name)
    @show_image()
    @show_tapping()
    @_render_overlay()
    @el.show()
    this

  _render_overlay: =>
    if @model.counters()
      @$('.counters').fadeIn().text(@model.counters())

    if @model.toughness() or @model.power()
      @$('.power').fadeIn().text("#{@model.power() || 0}/#{@model.toughness() || 0}")

  show_image: =>
    if  @model.covered()
      @img.attr('src','/images/back.jpg')
    else
      @img.attr('src',@model.image())

  show_tapping: =>
    if @model.tapped()
      $(@el).addClass('tapped')
    else
      $(@el).removeClass('tapped')


window.CardView = CardView